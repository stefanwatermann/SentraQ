#tag Class
Protected Class AuthenticationMySqlStoreProvider
Implements Authentication.IAuthenticationStoreProvider
	#tag Method, Flags = &h0
		Function GetValue(key as String) As String
		  // Part of the Authentication.IAuthenticationStoreProvider interface.
		  Var sql As String = "SELECT concat(Name, ':', IfNull(Roles, '')) from Users WHERE Hash = ?"
		  Var params() As Variant
		  params.Add(key)
		  Self.OpenDatabase
		  Var rs As RowSet = Self.Database.SelectSQL(sql, params)
		  For Each row As DatabaseRow In rs
		    Return row.ColumnAt(0).StringValue
		  Next
		  Self.Database.Close
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Function HasKey(key as string) As Boolean
		  // Part of the Authentication.IAuthenticationStoreProvider interface.
		  Var sql As String = "SELECT count(*) as cnt from Users WHERE Hash = ?"
		  Var params() As Variant
		  params.Add(key)
		  Self.OpenDatabase
		  Var rs As RowSet = Self.Database.SelectSQL(sql, params)
		  For Each row As DatabaseRow In rs
		    Return row.ColumnAt(0).IntegerValue = 1
		  Next
		  Self.Database.Close
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub Init(param as Variant)
		  // Part of the Authentication.IAuthenticationStoreProvider interface.
		  
		  // param = instance of MySQLCommunityServer
		  
		  If Not param IsA MySQLCommunityServer Then
		    Raise New RuntimeException("Param is not a MySQLCommunityServer.")
		  End
		  
		  Self.Database = param
		  
		  // validate database schema
		  Self.ValidateSchema()
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub OpenDatabase()
		  If Not Self.Database.IsConnected Then
		    Self.Database.Connect
		  end
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ValidateSchema()
		  // database must contain a table called "Users"
		  // with at least columns "Name", "Roles", "Hash" of type TEXT
		  
		  Self.OpenDatabase
		  
		  If Not Self.HasUsersTable Then
		    Raise New RuntimeException("Database file does not contain a 'Users' table.")
		  End
		  
		  If Not Self.HasUsersColumns Then
		    Raise New RuntimeException("'Users' table does not have all required columns (Name, Roles, Hash) of type TEXT.")
		  End
		  
		  Self.Database.Close
		End Sub
	#tag EndMethod


	#tag Note, Name = Readme
		MySql Database must have a table called "Users" and the table must have 
		at least three columns called "Name" varchar(100), "Roles" varchar(100), "Hash" varchar(255).
		Value of "Hash" shall contain the hash-code generated by the WebAuthentication.CreateUserHash(...)
		method for the user. 
		Important: Column "Hash" must be unique!
		
		Example DDL
		--------------
		CREATE TABLE `users` (
		  `Id` bigint NOT NULL AUTO_INCREMENT,
		  `Name` varchar(100) NOT NULL,
		  `Roles` varchar(100) DEFAULT NULL,
		  `Hash` varchar(255) NOT NULL,
		  PRIMARY KEY (`Id`),
		  UNIQUE KEY `users_unique` (`Hash`)
		)
		
		Example initialization
		---------------------
		Var authProvider As IAuthenticationStoreProvider = New AuthenticationMySqlStoreProvider
		Var mySqlDb As New MySQLCommunityServer
		mySqlDb.Host = "hostname"
		mySqlDb.DatabaseName = "dbname"
		mySqlDb.UserName = "user"
		mySqlDb.Password = "pwd"
		authProvider.Init(mySqlDb)
		
		Self.Authenticator = New WebAuthentication(kAuthenticationSalt, authProvider, DateTime.Now.AddInterval(0, 0, 5))
	#tag EndNote


	#tag Property, Flags = &h21
		Private Database As MySQLCommunityServer
	#tag EndProperty

	#tag ComputedProperty, Flags = &h21
		#tag Getter
			Get
			  Var cols() As String
			  For Each row As DatabaseRow In Self.Database.TableColumns("Users")
			    Var col As DatabaseColumn = row.Column("ColumnName")
			    If col.Type = 5 Or col.Type = 18 Then
			      // 5 = Text or VarChar, 18 = String
			      cols.Add(col.StringValue)
			    End
			  Next
			  Var allCols As String =  String.FromArray(cols, ", ")
			  Return allCols.Contains("Name") And allCols.Contains("Roles") And allCols.Contains("Hash")
			End Get
		#tag EndGetter
		Private HasUsersColumns As Boolean
	#tag EndComputedProperty

	#tag ComputedProperty, Flags = &h21
		#tag Getter
			Get
			  For Each row As DatabaseRow In Self.Database.Tables
			    If row.ColumnAt(0).StringValue = "Users" Then
			      Return True
			    End
			  Next
			  Return False
			End Get
		#tag EndGetter
		Private HasUsersTable As Boolean
	#tag EndComputedProperty


	#tag ViewBehavior
		#tag ViewProperty
			Name="Name"
			Visible=true
			Group="ID"
			InitialValue=""
			Type="String"
			EditorType=""
		#tag EndViewProperty
		#tag ViewProperty
			Name="Index"
			Visible=true
			Group="ID"
			InitialValue="-2147483648"
			Type="Integer"
			EditorType=""
		#tag EndViewProperty
		#tag ViewProperty
			Name="Super"
			Visible=true
			Group="ID"
			InitialValue=""
			Type="String"
			EditorType=""
		#tag EndViewProperty
		#tag ViewProperty
			Name="Left"
			Visible=true
			Group="Position"
			InitialValue="0"
			Type="Integer"
			EditorType=""
		#tag EndViewProperty
		#tag ViewProperty
			Name="Top"
			Visible=true
			Group="Position"
			InitialValue="0"
			Type="Integer"
			EditorType=""
		#tag EndViewProperty
	#tag EndViewBehavior
End Class
#tag EndClass
